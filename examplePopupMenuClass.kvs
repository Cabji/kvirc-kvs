/* note: this code is designed for copy & paste, then executing in Script Tester

   this example demonstrates how to: 
     1. build a popupmenu object ($new, $insertItem, $insertSeparator)
     2. make it a child of a main widget window ($new)
     3. employ privateimpl slot to show the popupmenu widget on screen (privateimpl, mousePressEvent, $exec)
     4. employ privateimpl slot to handle when an item is selected from the popupmenu widget (privateimpl, objects.connect)
     5. display the main widget on screen ($show)
     
   please note that this code is for example only, and you would probably design your code differently when using classes.
*/

// create a base widget
%Kapp = $new(widget)

// create a submenu for the popup menu
%subMenu = $new(popupmenu)
%submenu->$setTitle("Sub Menu")
// assign the returned uid to a hash key, and give it a string name we can use in the slot (handler)
%PumIds{"%subMenu->$insertItem("Extra stuff 1", 302)"} = "Extra stuff 1"
%PumIds{"%subMenu->$insertItem("Extra stuff 2", 303)"} = "Extra stuff 2"
%PumIds{"%subMenu->$insertItem("Extra stuff 3", 304)"} = "Extra stuff 3"
%subMenu->$insertSeparator(0)
%PumIds{"%subMenu->$insertItem("Extra stuff 4", 305)"} = "Extra stuff 4"
%PumIds{"%subMenu->$insertItem("Extra stuff 5", 306)"} = "Extra stuff 5"

// create the primary popup menu, child of %Kapp widget
%Kapp->%pumOppClick = $new(popupmenu, %Kapp)
%Kapp->%pumOppClick->$setTitle("Popup Menu")
%PumIds{"%Kapp->%pumOppClick->$insertItem("File", $icon("file"))"} = "File"
%PumIds{"%Kapp->%pumOppClick->$insertItem("Edit", $icon("editor"))"} = "Edit"
%PumIds{"%Kapp->%pumOppClick->$insertItem("Help", $icon("help"))"} = "Help"

// add the submenu to the primary menu
%Kapp->%pumOppClick->$addMenu(%submenu, 0)

// when we click the mouse, the popup menu will appear
privateimpl (%Kapp, mousePressEvent)
{
	// use $0 to determine which mouse button was clicked: 0 = primary, 1 = opposite, 2 = scroll/other
	if ($0 == 1) {@%pumOppClick->$exec();}
}

// when we click something in the popup menu, this handler will execute
privateimpl (%Kapp, handlePopupEvent)
{
	// $0 will contain the uid of the popup menu item that was clicked
	debug -c "Popup Menu Item: %PumIds{$0}";
}

// connect the primary popupmenu's "activated" signal to the %Kapp's "handlePopupEvent" slot
objects.connect %Kapp->%pumOppClick activated %Kapp handlePopupEvent

// display the base widget on screen
%Kapp->$show()
